import "pkg:/source/promises.bs"

namespace tests
	@SGNode("test")
	@suite
	class PromisesTests extends rooibos.BaseTestSuite

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promise tests")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@it("create promise")
		sub _()
			promise = promises.create()
			m.assertTrue(promises.isPromise(promise))
		end sub

		@it("promise validation")
		sub _()
			m.assertTrue(promises.isPromise(promises.create()))
			promiseNode = createNode("node", { promiseState: 0 })
			m.assertTrue(promises.isPromise(promiseNode))
			notPromise = createNode()
			m.assertFalse(promises.isPromise(notPromise))
			m.assertFalse(promises.isPromise(invalid))
		end sub

		@it("promise settlement check")
		sub _()
			m.assertFalse(promises.isComplete(promises.create()))
			m.assertTrue(promises.isComplete(promises.resolve({})))
			m.assertTrue(promises.isComplete(promises.reject({})))
			m.assertFalse(promises.isComplete(createNode()))
			m.assertFalse(promises.isComplete(invalid))
		end sub

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.chain()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("promise chain")
		sub _()
			context = {
				thenCount: 0
				catchCount: 0
				finallyCount: 0
			}

			promises.chain(promises.resolve(1), context).then(sub(result, context)
				context.thenCount++
				m.testSuite.assertEqual(result, 1)
			end sub).catch(sub(error, context)
				context.catchCount++
				m.testSuite.fail("should not get here")
			end sub).finally(sub(context)
				context.finallyCount++
				m.testSuite.assertEqual(context, {
					thenCount: 1
					catchCount: 0
					finallyCount: 1
				})
				m.testSuite.done()
			end sub)
		end sub

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.all()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("handled non-array")
		sub _()
			promises.chain(promises.all(invalid)).then(sub(result)
				m.testSuite.fail("should not get here")
			end sub).catch(sub(error)
				m.testSuite.assetEqual(error.message, "did not supply an array")
				m.testSuite.assetNotInvalid(error.backtrace)
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handled empty array")
		sub _()
			promises.chain(promises.all([])).then(sub(result)
				m.testSuite.assertEqual(result, [])
			end sub).catch(sub(error)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("resolving all promises")
		sub _()
			promises.chain(promises.all([
				promises.resolve(1)
				promises.resolve(2)
				promises.resolve(3)
			])).then(sub(_)
				msg = "resolved promise result should be [1,2,3]"
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [1, 2, 3]), msg)
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("resolving works with non-promise entire all promises")
		sub _()
			promises.chain(promises.all([
				promises.resolve(1)
				2
				promises.resolve(3)
			])).then(sub(_)
				msg = "resolved promise result should be [1,2,3]"
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [1, 2, 3]), msg)
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("resolving works with all non-promise entires all promises")
		sub _()
			promises.chain(promises.all([
				1
				2
				3
			])).then(sub(_)
				msg = "resolved promise result should be [1,2,3]"
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [1, 2, 3]), msg)
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("rejecting all promises")
		sub _()
			promises.chain(promises.all([
				promises.resolve(1)
				promises.reject(2)
				promises.resolve(3)
			])).then(sub(_)
				m.testSuite.fail("should not get here")
			end sub).catch(sub(_)
				msg = "rejected promise result should be 2"
				m.testSuite.assertEqual(_, 2, msg)
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub


		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.allSettled()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("handled non-array")
		sub _()
			promises.chain(promises.allSettled(invalid)).then(sub(result)
				m.testSuite.fail("should not get here")
			end sub).catch(sub(error)
				m.testSuite.assetEqual(error.message, "did not supply an array")
				m.testSuite.assetNotInvalid(error.backtrace)
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handled empty array")
		sub _()
			promises.chain(promises.allSettled([])).then(sub(result)
				m.testSuite.assertEqual(result, [])
			end sub).catch(sub(error)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("resolving all promises in allSettled")
		sub _()
			promises.chain(promises.allSettled([
				promises.resolve(1)
				promises.resolve(2)
				promises.resolve(3)
			])).then(sub(_)
				msg = "resolved promise result should be [1,2,3]"
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [
					{ status: promises.PromiseState.resolved, value: 1 },
					{ status: promises.PromiseState.resolved, value: 2 },
					{ status: promises.PromiseState.resolved, value: 3 }
				]), msg)
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("resolving works with non-promise entire in allSettled")
		sub _()
			promises.chain(promises.allSettled([
				promises.resolve(1)
				2
				promises.resolve(3)
			])).then(sub(_)
				msg = "resolved promise result should be [1,2,3]"
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [
					{ status: promises.PromiseState.resolved, value: 1 },
					{ status: promises.PromiseState.resolved, value: 2 },
					{ status: promises.PromiseState.resolved, value: 3 }
				]), msg)
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("resolving works with all non-promise entire in allSettled")
		sub _()
			promises.chain(promises.allSettled([
				1
				2
				3
			])).then(sub(_)
				msg = "resolved promise result should be [1,2,3]"
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [
					{ status: promises.PromiseState.resolved, value: 1 },
					{ status: promises.PromiseState.resolved, value: 2 },
					{ status: promises.PromiseState.resolved, value: 3 }
				]), msg)
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("rejecting a promise in allSettled")
		sub _()
			promises.chain(promises.allSettled([
				promises.resolve(1)
				promises.reject(2)
				promises.resolve(3)
			])).then(sub(_)
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [
					{ status: promises.PromiseState.resolved, value: 1 },
					{ status: promises.PromiseState.rejected, reason: 2 },
					{ status: promises.PromiseState.resolved, value: 3 }
				]))
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("rejecting all promises in allSettled")
		sub _()
			promises.chain(promises.allSettled([
				promises.reject(1)
				promises.reject(2)
				promises.reject(3)
			])).then(sub(_)
				m.testSuite.assertTrue(rooibos.common.eqArray(_, [
					{ status: promises.PromiseState.rejected, reason: 1 },
					{ status: promises.PromiseState.rejected, reason: 2 },
					{ status: promises.PromiseState.rejected, reason: 3 }
				]))
			end sub).catch(sub(_)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub


		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.any()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("handled non-array")
		sub _()
			promises.chain(promises.any(invalid)).then(sub(result)
				m.testSuite.fail("should not get here")
			end sub).catch(sub(error)
				m.testSuite.assetEqual(error.message, "did not supply an array")
				m.testSuite.assetNotInvalid(error.backtrace)
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handled empty array")
		sub _()
			promises.chain(promises.any([])).then(sub(result)
				m.testSuite.assertEqual(result, [])
			end sub).catch(sub(error)
				m.testSuite.fail("should not get here")
			end sub).finally(sub()
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handled a promise that resolves")
		sub _()
			promiseArray = [
				promises.create()
				promises.create()
				promises.create()
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.assertEqual(result, 2)
			end sub).catch(sub(_, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).finally(sub(promiseArray)
				promises.resolve(invalid, promiseArray[0])
				promises.resolve(invalid, promiseArray[2])

				m.testSuite.done()
			end sub)

			promises.resolve(2, promiseArray[1])
		end sub

		@async
		@it("handles a pre-resolved promise")
		sub _()
			promiseArray = [
				promises.create()
				promises.resolve(2)
				promises.create()
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.assertEqual(result, 2)
			end sub).catch(sub(_, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).finally(sub(promiseArray)
				promises.resolve(invalid, promiseArray[0])
				promises.resolve(invalid, promiseArray[2])

				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handles a non-promise value amongst pending promises")
		sub _()
			promiseArray = [
				promises.create()
				2
				promises.create()
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.assertEqual(result, 2)
			end sub).catch(sub(_, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).finally(sub(promiseArray)
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handles a non-promise value amongst rejected promises")
		sub _()
			promiseArray = [
				promises.reject(1)
				2
				promises.reject(2)
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.assertEqual(result, 2)
			end sub).catch(sub(_, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).finally(sub(promiseArray)
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handles a first pre-resolved promise")
		sub _()
			promiseArray = [
				promises.resolve(1)
				promises.resolve(2)
				promises.resolve(2)
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.assertEqual(result, 1)
			end sub).catch(sub(_, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).finally(sub(promiseArray)
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handles first pre-resolved promise along with a non-promise value")
		sub _()
			promiseArray = [
				promises.resolve(1)
				2
				promises.resolve(2)
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.assertEqual(result, 1)
			end sub).catch(sub(_, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).finally(sub(promiseArray)
				m.testSuite.done()
			end sub)
		end sub

		@async
		@it("handles all promises being pre-rejected")
		sub _()
			promiseArray = [
				promises.reject("1")
				promises.reject("2")
				promises.reject("3")
			]

			promises.chain(promises.any(promiseArray), promiseArray).then(sub(result, promiseArray)
				m.testSuite.fail("should not get here")
			end sub).catch(sub(error, promiseArray)
				m.testSuite.assertEqual(error.message, "All promises were rejected")
				m.testSuite.assertEqual(error.errors, ["1", "2", "3"])
				m.testSuite.assertNotInvalid(error.backtrace)
			end sub).finally(sub(promiseArray)
				m.testSuite.done()
			end sub)
		end sub


		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.onThen()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("thenable promise")
		sub _()
			promises.onThen(promises.resolve("thenable"), sub(_)
				m.testSuite.assertTrue(_ = "thenable", "resolved " + _)
				m.testSuite.done()
			end sub)
		end sub

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.onCatch()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("catchable promise")
		sub _()
			promises.onCatch(promises.reject("catchable"), sub(_)
				m.testSuite.assertTrue(_ = "catchable", "rejected " + _)
				m.testSuite.done()
			end sub)
		end sub

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.onFinally()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("finalable promise")
		sub _()
			promises.onFinally(promises.resolve("finalable"), sub()
				m.testSuite.assertTrue(true, "settled")
				m.testSuite.done()
			end sub)
		end sub

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("does not resolve to soon or too late")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("timer promise")
		function _()
			timerDurationInMillis = 125
			promise = sleepPromise(timerDurationInMillis / 1000)
			promises.onThen(promise, sub(_ as dynamic, ctx as dynamic)
				elapsedTimeInMillis = ctx.timespan.totalMilliseconds()
				? "elapsed time to resolve promise:" + elapsedTimeInMillis.tostr()
				tolerance = ctx.timerDurationInMillis * 0.2
				msg = "did not settle within 10% tolerance of timer duration"
				m.testSuite.assertTrue(ctx.timerDurationInMillis - tolerance <= elapsedTimeInMillis, msg)
				m.testSuite.assertTrue(ctx.timerDurationInMillis + tolerance >= elapsedTimeInMillis, msg)
				m.testSuite.done()
			end sub, {
				timespan: createObject("roTimespan")
				timerDurationInMillis: timerDurationInMillis
			})
		end function

		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
		@describe("promises.resolve()/promises.reject()")
		'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

		@async
		@it("resolved invalid")
		function _()
			promise = promises.resolve(invalid)
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, invalid)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject invalid")
		function _()
			promise = promises.reject(invalid)
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, invalid)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved integer")
		function _()
			promise = promises.resolve(1)
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, 1)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject integer")
		function _()
			promise = promises.reject(1)
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, 1)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved float")
		function _()
			promise = promises.resolve(1.1)
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, 1.1)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject float")
		function _()
			promise = promises.reject(1.1)
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, 1.1)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved boolean")
		function _()
			promise = promises.resolve(true)
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, true)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject boolean")
		function _()
			promise = promises.reject(true)
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, true)
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved string")
		function _()
			promise = promises.resolve("my string")
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, "my string")
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject string")
		function _()
			promise = promises.reject("my string")
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, "my string")
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved array")
		function _()
			promise = promises.resolve([1, 2, 3])
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, [1, 2, 3])
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject array")
		function _()
			promise = promises.reject([1, 2, 3])
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, [1, 2, 3])
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved AA")
		function _()
			promise = promises.resolve({
				key: "value"
			})
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, {
					key: "value"
				})
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject AA")
		function _()
			promise = promises.reject({
				key: "value"
			})
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, {
					key: "value"
				})
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved AA with subtype")
		function _()
			promise = promises.resolve({
				subType: "Node"
			})
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, {
					subType: "Node"
				})
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject AA with subtype")
		function _()
			promise = promises.reject({
				subType: "Node"
			})
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, {
					subType: "Node"
				})
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved AA with children array")
		function _()
			promise = promises.resolve({
				children: [{ subType: "Node" }]
			})
			promises.onThen(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, {
					children: [{ subType: "Node" }]
				})
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("reject AA with children array")
		function _()
			promise = promises.reject({
				children: [{ subType: "Node" }]
			})
			promises.onCatch(promise, sub(result as dynamic)
				m.testSuite.assertEqual(result, {
					children: [{ subType: "Node" }]
				})
				m.testSuite.done()
			end sub)
		end function

		@async
		@it("resolved SgNode")
		function _()
			testNode = createNode("Node")
			promise = promises.resolve(testNode)
			promises.onThen(promise, sub(result as dynamic, context as dynamic)
				m.testSuite.assertTrue(context.isSameNode(result))
				m.testSuite.done()
			end sub, testNode)
		end function

		@async
		@it("reject SgNode")
		function _()
			testNode = createNode("Node")
			promise = promises.reject(testNode)
			promises.onCatch(promise, sub(result as dynamic, context as dynamic)
				m.testSuite.assertTrue(context.isSameNode(result))
				m.testSuite.done()
			end sub, testNode)
		end function

		@async(60000)
		@it("unravels deep promise chain without crashing due to stackoverflow")
		function _()
			'this function creates a promise that depends on another promise (until we hit a max)
			doWork = function(context)
				'if we hit the max, resolve the promise and unravel the entire stack
				if context.currentCount > 10000
					return promises.resolve(true)
				end if
				context.currentCount = context.currentCount + 1

				'return a promise that depends on another future promise
				return promises.onThen(promises.resolve(true), function(result, context)
					doWork = context.doWork
					return doWork(context)
				end function, context)
			end function

			promises.chain(promises.resolve(true), {
				currentCount: 0,
				doWork: doWork
			}).then(function(result, context)
				doWork = context.doWork
				return doWork(context)
			end function).then(function(result, context)
				m.testSuite.done()
			end function).catch(function(error, context)
				print "error", error, FormatJson(error.backtrace)
			end function).toPromise()

		end function
	end class
end namespace

function createNode(nodeType = "Node" as string, fields = {} as dynamic) as object
	node = createObject("roSGNode", nodeType)
	node.update(fields, true)
	return node
end function

function sleepPromise(duration = 0.0001 as float) as dynamic
	promise = promises.create()
	promises.internal.delay(sub(promise as dynamic)
		promises.resolve(true, promise)
	end sub, promise, duration)
	return promise
end function
